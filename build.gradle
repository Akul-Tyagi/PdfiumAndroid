buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 1
        versionName "1.9.3-16kb"
        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Conditional external native build based on property
    if (project.hasProperty('android.disableExternalNativeBuild') && 
        Boolean.parseBoolean(project.property('android.disableExternalNativeBuild').toString())) {
        println "Skipping external native build, using pre-compiled libraries"
    } else {
        externalNativeBuild {
            ndkBuild {
                path "src/main/jni/Android.mk"
                arguments "APP_LDFLAGS+=-Wl,--page-size=16384"
            }
        }
    }

    sourceSets{
        main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

// Maven Publishing
ext {
    PUBLISH_GROUP_ID = 'com.github.Akul-Tyagi'
    PUBLISH_VERSION = "1.9.3-16kb"
    PUBLISH_ARTIFACT_ID = 'pdfium-android'
}

// For JitPack, we'll use a simpler publishing configuration
if (hasProperty('android.disableExternalNativeBuild')) {
    apply plugin: 'maven-publish'
    
    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    groupId = 'com.github.Akul-Tyagi'
                    artifactId = 'pdfium-android'
                    version = '1.9.3-16kb'
                }
            }
        }
    }
} else {
    // Only apply the Maven Central publishing script for non-JitPack builds
    if (file("${rootProject.projectDir}/publish-mavencentral.gradle").exists()) {
        apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"
    }
}
